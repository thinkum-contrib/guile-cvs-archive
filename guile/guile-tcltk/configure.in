dnl Process this file with autoconf to produce a configure script.
AC_INIT(guile-tcl.c)
AM_INIT_GUILE_MODULE(gtcltk-lib)
AM_MAINTAINER_MODE

#--------------------------------------------------------------------
#
# User options
#
#--------------------------------------------------------------------

AC_ARG_WITH(tcltk,
[  --with-tcltk=DIR        Use DIR/include and DIR/lib for tk and tcl])

AC_ARG_WITH(tcl,
changequote(<, >)dnl
<  --with-tcl=LIBNAME      Name of tcl library to use [LIBNAME=tcl7.5]>,
changequote([, ])dnl
)

AC_ARG_WITH(tcl,
changequote(<, >)dnl
<  --with-tk=LIBNAME       Name of tk library to use [LIBNAME=tk4.1]>,
changequote([, ])dnl
)

#--------------------------------------------------------------------

AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL

if test "$with_tcltk" = no; then
  GTCLTK=
else

if test -z "$with_tcltk" || test "$with_tcltk" = yes; then
### Some compilers don't check /usr/local/include and /usr/local/lib
### by default.  However, that's where Tcl and Tk are usually installed. 
test "$GCC" = yes || {
  test -d /usr/local/include && TCLTK_INCLUDE="-I/usr/local/include"
  test -d /usr/local/lib     && TCLTK_LIBDIR="-L/usr/local/lib"
}
else
  TCLTK_INCLUDE="-I$with_tcltk/include"
  TCLTK_LIBDIR="-L$with_tcltk/lib"
fi

if test -n "$with_tcl"; then
  TCLNAME=$with_tcl
else
  TCLNAME=tcl7.5
fi

if test -n "$with_tk"; then
  TKNAME=$with_tk
else
  TKNAME=tk4.1
fi

dnl Checks for libraries.
CFLAGS="$TCLTK_INCLUDE $TCLTK_LIBDIR $CFLAGS"

AC_PATH_X
AC_PATH_XTRA
AC_CHECK_LIB(X11, main)

### On some systems, the Tcl library contains references to
### functions in the dynamic linker library, -ldl, so we may need
### to include that.
AC_CHECK_LIB(dl, dlopen, TCL_EXTRA_LIBS='-ldl', TCL_EXTRA_LIBS='')

### On some systems, the Tcl library contains references to
### functions in the socket library, -lsocket, so we may need
### to include that.
AC_CHECK_LIB(socket, main, TCL_EXTRA_LIBS="$TCL_EXTRA_LIBS -lsocket")

### On some systems, the Tcl library contains references to
### functions in the nsl library, -lnsl, so we may need
### to include that.
AC_CHECK_LIB(nsl, main, TCL_EXTRA_LIBS="$TCL_EXTRA_LIBS -lnsl")

### Use gtcltk-lib only if we seem to have Tcl installed on the
### system.  We really should check for Tk as well, but that involves
### finding all the X libraries that we need to do a complete link.
AC_CHECK_LIB($TCLNAME, Tcl_CreateInterp, GTCLTK=libgtcltk.la, GTCLTK=, $TCL_EXTRA_LIBS -lm)

fi

if test -z "$with_tcltk"; then
  echo   '*************************************************************'
  if test -z "$GTCLTK"; then
    echo '*  Couldn'\''t link with Tk.  Configuring without Tk support.  *'
  else
    echo '*  Configuring with Tk support.                             *'
  fi
  echo   '*************************************************************'
else
  if test -z "$GTCLTK" && test ! "$with_tcltk" = no; then
    echo '############################################################'
    echo '#                                                          #'
    echo '#  Configuration of gtcltk-lib failed.  Check `config.log'\'' #'
    echo '#  (Remember to "rm config.cache" if you try again!)       #'
    echo '#                                                          #'
    echo '############################################################'
    exit 2
  fi
fi

AC_SUBST(GTCLTK)

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_SUBST(TCL_EXTRA_LIBS)

### Make PLUGIN system files
(
  test -d PLUGIN || mkdir PLUGIN
  cd PLUGIN
  > tmpinit
  if test -n "$GTCLTK"; then
    echo 'scm_init_gtcl scm_init_gtk' >> tmpinit
  fi
  mv tmpinit guile.inits

  > tmpinit
  if test -n "$GTCLTK"; then
    echo "-lgtcltk -l$TKNAME" >> tmpinit
    echo "-l$TKNAME -l$TCLNAME" >> tmpinit
    echo "-l$TCLNAME -lguile" >> tmpinit
    echo "-l$TCLNAME -lm"     >> tmpinit
  fi
  mv tmpinit guile.libs

  > tmpinit
  if test -n "$GTCLTK"; then
    echo "xtra_cflags=\"$TCLTK_INCLUDE $TCLTK_LIBDIR $X_CFLAGS \$xtra_cflags\"" >> tmpinit
    echo "xtra_libs=\"$X_PRE_LIBS $X_LIBS $LIBS $X_EXTRA_LIBS $TCL_EXTRA_LIBS \$xtra_libs\"" >> tmpinit
    echo 'xtra_dependencies="../gtcltk-lib/libgtcltk.la $xtra_dependencies"' >> tmpinit
    echo 'libtool_libs="-lgtcltk $libtool_libs"' >> tmpinit
  fi
  mv tmpinit guile.config
)

AC_OUTPUT(Makefile)
