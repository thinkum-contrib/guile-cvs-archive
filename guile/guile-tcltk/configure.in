dnl Process this file with autoconf to produce a configure script.

AC_INIT(guile-tcl.c)
AM_INIT_GUILE_MODULE(gtcltk-lib)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB

### Some compilers don't check /usr/local/include and /usr/local/lib
### by default.  However, that's where Tcl and Tk are usually installed. 
test $gcc = yes || {
  test -d /usr/local/include && CFLAGS="${CFLAGS} -I/usr/local/include"
  test -d /usr/local/lib     && CFLAGS="${CFLAGS} -L/usr/local/lib"
}

dnl Checks for libraries.
AC_PATH_XTRA

### On some systems, the Tcl library contains references to
### functions in the dynamic linker library, -ldl, so we may need
### to include that.
AC_CHECK_LIB(dl, dlopen, TCL_EXTRA_LIBS='-ldl', TCL_EXTRA_LIBS='')

### On some systems, the Tcl library contains references to
### functions in the socket library, -lsocket, so we may need
### to include that.
AC_CHECK_LIB(socket, main, TCL_EXTRA_LIBS="$TCL_EXTRA_LIBS -lsocket")

### On some systems, the Tcl library contains references to
### functions in the nsl library, -lnsl, so we may need
### to include that.
AC_CHECK_LIB(nsl, main, TCL_EXTRA_LIBS="$TCL_EXTRA_LIBS -lnsl")

### Use gtcltk-lib only if we seem to have Tcl installed on the
### system.  We really should check for Tk as well, but that involves
### finding all the X libraries that we need to do a complete link.
AC_CHECK_LIB(tcl7.5, Tcl_CreateInterp, GTCLTK=libgtcltk.a, GTCLTK=, $TCL_EXTRA_LIBS -lm)
AC_SUBST(GTCLTK)

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_SUBST(TCL_EXTRA_LIBS)
AC_OUTPUT(Makefile PLUGIN/Makefile)
